plugins {
    id "com.github.hierynomus.license" version "0.15.0"
    id "org.sonarqube" version "3.0"
}

// Apply the java plugin to add support for Java
apply plugin: 'java'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

// Apply the checkstyle plugin
apply plugin: 'checkstyle'
checkstyle {
    toolVersion '8.26'
    configFile file("config/checkstyle.xml")
    maxErrors(0)
    maxWarnings(0)
}
checkstyleMain {
    source ='src/main/java'
}
checkstyleTest {
    source ='src/test/java'
}

sonarqube {
    properties {
        property "sonar.projectKey", "iluwatar_30-seconds-of-java"
        property "sonar.organization", "iluwatar"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

license {
    header project.file('LICENSE')
    include '**/*.java'
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.12'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

test {
    useJUnitPlatform()
}

task licenseFormatJava(type:nl.javadude.gradle.plugins.license.License) {
    source = fileTree('src/main/java')
}

task licenseTestJava(type:nl.javadude.gradle.plugins.license.License) {
    source = fileTree('src/main/java')
    check = true
}

licenseFormatMain.enabled = false
licenseMain.enabled = false

licenseFormat.dependsOn licenseFormatJava
licenseTest.dependsOn licenseTestJava
